group 'org.jetbrains'
version '1.0-SNAPSHOT'

buildscript {
    apply from: 'versions.gradle'

    repositories {
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.moowork.gradle:gradle-node-plugin:$node_plugin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_plugin_version"
    }
}

subprojects {
    apply plugin: 'kotlin2js'
    apply plugin: 'com.moowork.node'

    repositories {
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        jcenter()
        maven {
            url "http://dl.bintray.com/jetbrains/kotlin-wrappers"
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
        compile "org.jetbrains.kotlinx:kotlinx-html-js:$kotlinx_html_version"
    }

    def projectName = name

    compileKotlin2Js {
        kotlinOptions {
            outputFile = "$projectDir/build/classes/main/${projectName}.js"
            moduleKind = "commonjs"
            sourceMap = true
        }
    }

    task processPkg(type: Copy) {
        from '.'
        into 'build/npm'
        include 'package.json'
        expand(
            kotlin_version: "$kotlin_version",
            kotlinext_version: "$kotlinext_version",
            mocha_version: "$mocha_version",
            react_version: "$react_version",
            react_dom_version: "$react_dom_version",
        )
    }

    task prepublish(type: Copy) {
        from '.'
        into 'build/npm'
        exclude 'package.json'
        exclude 'build/npm'
    }

    npm_publish {
        args = ['--access', 'public']
        execOverrides {
            it.workingDir = 'build/npm'
        }
    }
    npm_publish.dependsOn prepublish
    npm_publish.dependsOn processPkg
    prepublish.dependsOn build

    ext.configurePublishing = { baseVersion ->
        apply plugin: 'com.jfrog.bintray'
        apply plugin: 'maven-publish'
        apply plugin: 'java'

        def v = "$baseVersion-kotlin-$kotlin_version"
        bintray {
            user = System.getenv('BINTRAY_USER')
            key = System.getenv('BINTRAY_KEY')
            publish = true
            pkg {
                repo = 'kotlin-wrappers'
                name = projectName
                userOrg = 'jetbrains'
                licenses = ['Apache-2.0']
                vcsUrl = 'https://github.com/JetBrains/kotlin-wrappers.git'
                version {
                    name = v
                }
            }
            publications = ['Publication']
        }

        publishing {
            publications {
                Publication(MavenPublication) {
                    from components.java
                    groupId 'org.jetbrains'
                    artifactId projectName
                    version v
                }
            }
        }
    }
}
