import org.gradle.api.publish.maven.internal.artifact.FileBasedMavenArtifact

apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
    js()
    jvm()

    sourceSets {
        commonMain {
            kotlin.srcDirs += "src/main/kotlin"
            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
            }

        }
        commonTest {
            kotlin.srcDirs += "src/test/kotlin"
            dependencies {
                api "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
                api "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
            }
        }
        jvmMain {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
            }
        }
        jvmTest {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
                api "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
            }
        }
        jsMain {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
                api project(":kotlin-extensions")
            }
        }
        jsTest {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
                api "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
            }
        }
    }
}

configurePublishing(css_version)

project("kotlin-css-js") {
    applyKotlinJS()

    configurePublishingWithNPM(css_version)

    dependencies {
        api project(":kotlin-css")
    }
}

// to publish gradle metadata
tasks.getByName("bintrayUpload") {
    doFirst {
        publishing.publications
                .withType(MavenPublication.class)
                .forEach { publication ->
                    def moduleFile = buildDir.toPath().resolve("publications/${publication.name}/module.json").toFile()
                    if (moduleFile.exists()) {
                        publication.artifact(new FileBasedMavenArtifact(moduleFile) {
                            String getDefaultExtension() {
                                return "module"
                            }
                        })
                    }
                }
    }
}

project("kotlin-css-jvm") {

    apply plugin: 'org.jetbrains.kotlin.jvm'

    configurePublishingWithNPM(css_version)

    dependencies {
        api project(":kotlin-css")
    }
}
